plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
}

ext {
    set('springBootVersion', "3.1.2")
    set('springCloudVersion', "2022.0.4")
    set('springCloudAlibabaVersion', "2022.0.0.0")
}

repositories {
    mavenCentral()
}

// 子项目的通用配置
subprojects {
    // 使用插件
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    // 项目的默认名称和版本
    group = "com.example"
    version = '0.0.1-SNAPSHOT'

    // 指定JDK版本
    sourceCompatibility = '17'
    jar.enabled = true

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }


    // 指定仓库地址
    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
    }
    dependencyManagement {
        // 导入SpringBoot和SpringCloud依赖Bom
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }

    }
}
